Notes and analysis of the Sinclair Scientific ROM

EXP: exponent mask
DIGIT1, DIGIT: used with AKCN increment. MASK3 is 1. Don't overlap with 4.
MANT: the A value?
EXP_S: negative exponent
NEWEXP: E pressed, entering a new exponent

FLAGS:
A
0 ALL
1 (S9) FLAG1A
3 (S5) UP_LOW
5 (S10) LOW
7 (S8) TAN
11 (S4) COS_TAN

B
0 ALL
1 (S9) EMODE
6 (S6S7) FLAG6B ??two bits??
7 (S8) EMODE2

Constants
0: ....
1: .*.* 5
2: ....
3: ...* 1
4: ....
5: ...* 1
6: ...* 1
7: .*.* 5
8: ....
9: ...* 1
a: ...* 1
b: ...* 1
c: .*.* 5
d: ...* 1
e: .*.. 4
f: ....

Masks:
Rotated by 1 in ROM?:
0a987654321 :S value
00000000000 mask 0 all
 5          mask 1 flag1a, emode(b)
   00       mask 2
     1      mask 3 inc mask up_low(a)
0    000000 mask 4 a value
1           mask 5 low(a)
   01       mask 6 flag6b
  5         mask 7 negative exponent tan(a), emode2(b)
 000000     mask 8
 0001       mask 9
1    000000 mask a
      1     mask b cos_tan(a)
     00005  mask c
     00001  mask d
     4      mask e
     0      mask f inc mask

Masks:
SSEEMMMMM
a9876543210 :S value
00000000000 mask 0 all
5           mask 1 flag1a, emode(b)
  00        mask 2
    1       mask 3 inc mask up_low(a)
    0000000 mask 4 a value
          1 mask 5 low(a)
  01        mask 6 flag6b
 5          mask 7 negative exponent tan(a), emode2(b)
000000      mask 8
0001        mask 9
    0000001 mask a
     1      mask b cos_tan(a)
    00005   mask c
    00001   mask d
    4       mask e
    0       mask f inc mask

0   START    ZFA    ALL ; init - clear flags
1            ZFB    ALL
2            AKA    ALL ; clear A and C
3            AKC    ALL
4   MAINLOOP SLLA   MANT ; Restore mantissa
5            AKB    ALL ; clear B
6   WAITSCAN SYNC
7            SCAN
8            BINE   WAITSCAN
9   WAITKEY  WAITNO WAITED
10  WAITED2  SYNC
11           SCAN
12           BIE    WAITKEY ; loop if no key
13           SYNC
14           SRLA   MANT
15           BKO    LOWERKEY
16           BKO    PLUSKEY
17           BKO    MINUSKEY
18           BKO    DIVKEY
19           BKO    MULTKEY
20           BKO    UPPERKEY
21           BKO    EKEY
22           BKO    ZEROKEY
23           EXAB   ALL ; save A in B, A=0
24           AKCN   DIGIT1 ; count column into A
25           EXAB   ALL ; restore A, B holds count
26           BINE   MAINLOOP ; C pressed, not in row KN
27  ZEROKEY  TFB    EMODE ; B holds key 0-9
28           BINE   EDIGIT
29           TFB    FLAG6B ; if FLAG6B...
30           BIE    LABEL33
31           AKA    ALL ; then clear A and FLAG6B
32           ZFB    FLAG6B
33  LABEL33  ACKA   DIGIT ; C holds digit position
34  BSHIFT   SRLB   ALL ; shift B right C times.
35           SAKA   DIGIT1 ; decrement A
36           BIE    BSHIFT (no borrow)
37           ACKC   DIGIT1 ; increment C
38           AABA   MANT ; Append new digit
39           BIE    MAINLOOP ; done digit processing
40  EDIGIT   ZFB    NEWEXP ; clear new exponent flag
41           SLLA   EXP ; shift exponent left
42           SLLB   ALL ; shift digit left
43           AABA   EXP ; append new digit to exponent
44           BIE    MAINLOOP ; done
45  EKEY     SFB    EMODE ; entering exponent mode
46           SFB    NEWEXP
47           BIE    MAINLOOP ; done
48  LOWERKEY SFA    LOW ; LOW and UP_LOW set for LOWER key
49  UPPERKEY SFA    UP_LOW ; UP_LOW set for UPPER key
50           BIE    MAINLOOP ; done
51  MINUSKEY TFA    UP_LOW ; alt-MINUS is COS
52           BINE   COSKEY
53           TFB    NEWEXP ; - follows E?
54           BIE    SETNEG
55           AAKA   EXP_S ; if so, set minus digit
56           BIE    MAINLOOP
57  SETNEG   AAKA   MANT_S ; set minus digit
58           BIE    PLUSKEY ; clear condition flag?
59  PLUSKEY  TFA    UP_LOW ; alt-PLUS is SIN
60           BINE   SINKEY
61  ADDSUB   AKC    DIGIT ; Add A and C. clear C flag? sign?
62           ACKB   ALL ; copy C to B
63           ABAC   EXP ; add exponents into C
64           BIE    LABEL65 ; why?
65  LABEL65  ABAC   EXP_S ; add exponent signs
66           SCKC   EXP_S ; subtract 5 from sign
67           BIE    LABEL72 ; signs were different
68           CAB    EXP ; compare exponents
69           BIE    LABEL71 ; same
70           EXAB   ALL
71  LABEL71  SABC   EXP ; difference in exponents
72  LABEL72  CAK    EXP_S
73           BINE   LABEL75
74           EXAB   ALL
75  LABEL75  CAK    MANT1
76           BIE    BSHIFT2
77           EXAB   ALL
78  BSHIFT2  SCKC   EXP1 ; shift B by difference in exponents to line up
79           BINE   DONE2
80           SRLB   MANT
81           BIE    BSHIFT2
82  DONE2    ABAC   MANT_S ; add signs and subtract 5
83           SCKC   MANT_S
84           BIE    LABEL89 ; signs different
85  EXLOOP   AABA   MANT ; add mantissas
86           BIE    WRAPUP ; branch if no overflow
87           EXAB   MANT ; handle overflow
88           EXAB   MANT_S
89  LABEL89  SABA   MANT
90           BINE   EXLOOP
91           BIE    WRAPUP ; BET
92  LABEL92  CAB    EXP
93           BIE    LABEL95
94           EXAB   MASK9
95  LABEL95  SABA   EXP
96           BIE    LABEL114 ; BET - no borrow?
97  DIVKEY   TFA    UP_LOW ; entry point for DIV
98           BINE   TANKEY ; alt-DIV is TAN
99           SFA    COS_TAN ; why set COS? Doesn't make sense
100 LABEL100 SFA    RET1FLAG ; 'subroutine call' flag
101          BIE    WRAPUP ; BET
102 RET1     ZFA    RET1FLAG ; RET1FLAG means return here
103          AAKA   EXP_S
104          BIE    MULTKEY ; clear cc
105 MULTKEY  TFA    UP_LOW ; alt-MULT is LOG
106          BINE   LOGKEY
107          AKC    DIGIT
108          ACKB   ALL
109          EXAB   MANT
110          ABAC   EXP_S
111          SCKC   EXP_S
112          BIE    LABEL92
113          AABA   EXP
114 LABEL114 AABA   MANT_S
115          BIE    LABEL116 ; clear before test
116 LABEL116 TFA    COS_TAN
117 LABEL117 AKA    MANT
118          BINE   LABEL279 ; branch if COS or TAN
119 MLTLOOP1 SRLA   MANT ; multiply B * C
120 MLTLOOP2 CCK    MANTLOW1 ; Add B to A, repeat low digit of C times
121          BINE   LABEL125
122          SCKC   MANT1
123          AABA   MANT
124          BIE    MLTLOOP2
125 LABEL125 SRLC   MANT ; shift C right
126          CCK    MANT1
127          BIE    MLTLOOP1 ; loop if not done
128          TFA    LOW
129          BINE   LABEL308 ; Return to LABEL308 if LOW flag set (ANTILOG)
130 WRAPUP   CAK    MANT1 ; Test if mantissa is < 1
131          BINE   START ; if mantissa is 0, reset
132 MANTSHL  CAK    DIGIT1 ; Loop to left-align mantissa
133          BIE    MANTOK ; Branch if mantissa aligned
134          SLLA   MANT ; Put mantissa back into place
135          CAK    EXP_S ; Check sign
136          BIE    MANTNEG ; Branch if negative
137          SAKA   EXP1 ; Decrement exponent
138          BIE    LABEL141 ; Branch if no borrow
139          AKA    EXP_S ; Set exponent sign
140          AKA    EXP1 ; Set exponent to 1
141 LABEL141 SAKA   EXP1 ; Decrement exponent
142 MANTNEG  AAKA   EXP1 ; Increment exponent
143          BIE    WLOOP ; first will clear flag
144          BIE    WLOOP ; BET
145 MANTOK   SRLA   MANT
146          CAK    EXP_S ; Test exponent sign
147          BINE   EXPPOS
148          SAKA   EXP1 ; Decrement exponent
149          SAKA   EXP1 ; Decrement exponent
150 EXPPOS   AAKA   EXP1 ; Increment exponent
151          BIE    LABEL152 ; clear condition
152 LABEL152 TFA    RET1FLAG ; Subroutine return?
153          BINE   RET1 ; return to RET1 if RET1FLAG set
154          ZFB    ALL ; Clear B
155          SFB    FLAG6B ; Set op done flag?
156          ZFA    ALL ; Clear A flags
157          CAK    EXP1 ; Is exponent zero (< 1)
158          BIE    EXPPOS
159 LABEL159 AAKA   EXP_S ; Flip sign
160          BIE    LABEL159 ; Branch if sign was clear before
161 EXPPOS   AAKC   ALL ; Copy A to C
162          BIE    MAINLOOP ; BET
163 TANKEY   SFA    TAN
164 COSKEY   SFA    COS_TAN
165 SINKEY   SAKA   EXP1 ; shift mantissa by exponent
166          BINE   DONESHFT
167          SRLA   MANT
168          BIE    SINKEY
169 DONESHFT AKA    MASK9 ; ?
170          SAKA   MASK9
171          AKC    ALL ; clear C
172          TFA    LOW
173          BIE    LABEL180
174          TFA    TAN
175          BIE    LABEL179
176          AAKC   ALL
177          AKB    MASK11
178          BIE    TRGLOOP2
179 LABEL179 AAKA   MANTD1
180 LABEL180 AKB    MANTD5 ; add 5 to B mantissa
181          SRLB   MANT ; and shift
182          ACKC   MASK11 ; increment C
183 TRIGLOOP SAKA   MANTD1 ; main loop: iterate A times
184          BINE   TRIGDONE ; branch on borrow - done
185 TRGLOOP2 SLLC   ALL ;
186          SLLC   ALL
187          SLLC   ALL
188          ACKC   MANTD5 ; round C up
189          SCBC   ALL ; C-B -> C
190          SRLC   ALL ; shift and add to
191          SRLC   ALL
192          SRLC   ALL
193          SLLB   ALL ; shift B left 3
194          SLLB   ALL
195          SLLB   ALL
196          ACKC   MANTD5 ; round C up
197          ABCB   ALL ; B+C -> B
198          SCKC   MANTD5 ; restore C
199          SRLB   ALL ; shift B right 3
200          SRLB   ALL
201          SRLB   ALL
202          TFA    LOW ; ARC-op?
203          BIE    TRIGLOOP ; no: branch
204          AAKA   MASK9 ; ARC-op
205          TFA    TAN
206          BIE    ISSINCOS ; branch if SIN/COS
207          CCK    DIGIT1 ; doing TAN, compare C to K
208          BINE   TRGLOOP2 ; loop if not done
209          BIE    TRIG2 ; BET
210 ISSINCOS TFA    COS_TAN ; SIN/COS
211          BINE   ISCOS ; branch if COS
212          CAB    MANT ; must be SIN
213          BIE    TRGLOOP2
214          BIE    TRIG2
215 ISCOS    CAK    MANT1 ; COS
216          EXAB   MANT ; compare C-A the hard way
217          CCB    MANT
218          EXAB   MANT
219          BIE    TRGLOOP2 ; C==A
220 TRIG2    SRLA   ALL ; shift A right 5
221          SRLA   ALL
222          SRLA   ALL
223          SRLA   ALL
224          SRLA   ALL
225          BIE    WRAPUP ; done if A zero
226 TRIGDONE TFA    TAN
227          BIE    LABEL233
228          ACKA   ALL ; doing TAN
229          ABOC   ALL
230          AKB    ALL
231          ZFA    UP_LOW
232          BIE    LABEL100 ; do division??? what is condition here?
233 LABEL233 TFA    COS_TAN
234          BIE    LABEL236 ; branch for SIN
235          ACKB   ALL ; COS: return C, rounded
236 LABEL236 EXAB   ALL ; SIN: return B
237          BIE    WRAPUP ; BET (no overflow)
238 LOGKEY   TFA    LOW ; low-MULT is antilog
239          BIE    LOG
240 ANTILOG  CAK    EXP1 ; antilog - loop
241          BINE   LABEL248
242          CAK    EXP_S
243          BINE   LABEL247
244          SRLA   MANT
245          SAKA   EXP1
246          BIE    ANTILOG
247 LABEL247 SLLA   MANT
248 LABEL248 AKA    EXP
249          SLLA   MASK8
250          SLLA   MASK8
251 LOG      AKC    ALL ; This loop computes log(.1)/log(.99) = 229
252          ACKC   DIGIT1 ; C = 1000000
253 LOGLOOP  ACKB   MANT ; Compute log B*.99^C until < .1
254          SRLB   MANT
255          SRLB   MANT
256          SCBC   MANT ; C -= C / 100
257          TFB    NEWEXP
258          BINE   ALOGLOOP
259          ACKC   MASK9A ; Count number of iterations
260          CCK    MASK11 ; C vs 100000
261          BIE    LOGLOOP ; Loop if >=
262          SCBB   MASK9 ; Left over bit from B is .15
263          ABCC   MANT
264          SCKC   MASK11
265          SRLB   ALL ; Shift 229 into B mantissa
266          SRLB   ALL
267          SRLB   ALL
268          SRLB   ALL
269          ABCB   MANT ; add .15
270          EXAB   MANT ; 229.15 in A, arg in B
271          TFA    LOW
272          BINE   ANTILOG ; return to ANTILOG every time through if LOW set
273          ABOC   ALL ; Copy B (arg) to C
274          FFA    TAN ; using this flag for something?
275          TFA    TAN
276          BINE   LOGLOOP ; back to the loop
277          AAKC   MANT
278 LABEL278 AKA    MANT ; clear A mant, arg exp still in A
279 LABEL279 AKA    DIGIT4 ; start digit count at 4, iterate until 10
280 LOGDIV   CCB    MANT ; Compute C/B: C=-log(arg)/log(.99), B=-1/log(.99) = 229.15
281          BINE   SHIFTDIV ; C-B: branch if not less than
282          SCBC   MANT ; C -= B
283          AAKA   MANT1 ; A++
284          BIE    LOGDIV ; BET
285 SHIFTDIV AAKC   DIGIT1 ; shift remainder C left, increase mantissa
286          BINE   LABEL291 ; so C > B
287          SLLA   MANT
288          ACKA   DIGIT
289          SLLC   MANT
290          BIE    LOGDIV ; until count hits 10
291 LABEL291 AKA    DIGIT
292          TFA    UP_LOW ; pick the return target
293          BINE   LOGSKIP
294          BIE    WRAPUP
295 WAITED   BIE    WAITED2 ; inconvenient WAITNO target
296 LOGSKIP  AAKC   MANT ; A mantissa -> C
297          AKA    MANT ; clear A mantissa
298          SRLA   ALL ; shift exponent into top of mantissa (log of exponent)
299          SRLA   ALL
300          AKA    EXP ; clear exponent
301          BIE    ADDSUB ; BET (clear from AAKC)
302 ANTILOG  AAKC   MANT
303          AKA    MANT
304          AKA    MASK11
305          SABA   MANT
306          EXAB   MANT
307          BIE    LABEL117 ; Sub call to multiply by 229.15
308 LABEL308 SRLA   MANT
309          SFB    NEWEXP
310          BIE    LOG ; ? What is condition here?
311 ALOGLOOP SAKA   MANTD1
312          BIE    LOGLOOP
313          AAKA   MANTD1
314          SLLA   MANT
315          AAKA   MASK11
316          ABCC   MANT
317          EXAB   MANT
318          ZFA    UP_LOW
319          BINE   LABEL278 ; isn't flag zero after ZFA?? Maybe still NE from ABCC?
